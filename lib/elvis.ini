"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" DEFINE SOME DIGRAPHS
if os == "msdos" || os == "os2" || (os == "win32" && gui != "windows")
then source! (elvispath("elvis.pc8"))
else source! (elvispath("elvis.lat"))
"
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" CHOOSE SOME DEFAULT OPTION VALUES BASED ON THE INVOCATION NAME
let p=tolower(basename(program))
if p == "ex" || p == "edit"
then set! initialstate=ex
if p == "view"
then set! defaultreadonly
if p == "edit" || p == "vedit"
then set! novice
set p=""
if home == ""
then let home=dirdir(program)
"
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" IF ALIASES ARE SUPPORTED, THEN LOAD THE DEFAULT ALIASES
if feature("alias")
then source! (elvispath("elvis.ali"))
"
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" SYSTEM TWEAKS GO HERE
"
" The Linux console can't handle colors and underlining.
if gui=="termcap"
then {
 if term=="linux"
 then set! nottyunderline
}
"
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" WINDOWS DEFAULT COLORS GO HERE (may be overridden in elvis.rc file)
if gui=="windows"
then {
 color e green
 color i magenta
 color u blue
 color f red
}
"
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" X11 DEFAULT COLORS AND TOOLBAR GO HERE (may be overridden in .exrc file)
if gui=="x11"
then so! (elvispath("elvis.x11"))
"
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" EXECUTE THE STANDARD CUSTOMIZATION SCRIPTS
let f=(os=="unix" ? ".elvisrc" : "elvis.rc")
if $EXINIT
then eval $EXINIT
else source! (exists(home/f)?home/f:home/".exrc")
" ÷÷÷ PROCURA
source! ~/.elvislib/elvis.rc
if exrc && getcwd()!=home
then safer! (exists(f)?f:".exrc")
set f=""
"
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" X11 INTERFACE DEFAULT FONTS GO HERE
if gui == "x11"
then {
 if normalfont == ""
 then set! normalfont="*-courier-medium-r-*-18-*" 
 then set! boldfont="*-courier-bold-r-*-18-*" 
 then set! italicfont="*-courier-medium-o-*-18-*" 
}
"
if os == "os2" && gui != "x11"
then {
  if $TERM != "xterm"
  then color n white
  else color n black
  color e green
  color i magenta
  color u blue
  color f red
  set ul=20
  set ruler
  set showmatch
  set showmode
  set autoindent
}
