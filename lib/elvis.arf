"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" SAVE THE CURRENT STATE OF THE "saveregexp" OPTION
let s=saveregexp
set nosaveregexp
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" TAKE A GUESS AT THE BUFFER'S PREFERRED DISPLAY MODE
let e=tolower(dirext(filename))
if knownsyntax(filename)
then set! bufdisplay=syntax
if os=="unix" && buflines >= 1
then {
 1s/^#! *[^ ]*\/\([^ ]\+\).*/set! bufdisplay="syntax \1"/x
 if bufdisplay<<11=="syntax perl"
 then set! bufdisplay="syntax perl"
 if bufdisplay<<12=="syntax tclsh"
 then set! bufdisplay="syntax tcl"
}
if !newfile
then {
 if readeol=="binary" && bufdisplay=="normal"
 then set! bufdisplay=hex
 if e==".man"
 then set! bufdisplay=man
 if strlen(e)==2 && isnumber(e>>1) && buflines>=1
 then 1s/^\./set! bufdisplay=man/x
 if e==".tex"
 then set! bufdisplay=tex
 if e<<4==".htm" || e==".shtml" || e==".dhtml"
 then set! bufdisplay=html
 if e<<4==".sgm"
 then set! bufdisplay="html sgml"
 if buflines >= 1 && bufdisplay=="hex"
 then 1s/^<[HIThit!]/set! bufdisplay=html/x
 if (filename<<5=="http:" || filename<<4=="ftp:") && strlen(e)<4 && bd=="hex"
 then set! bufdisplay=normal
 if bufdisplay=="normal" && buflines >= 1
 then 1s/^From .*/set! bufdisplay="syntax email"/x
 if dirdir(filename)=="/tmp" || dirdir(filename)=="/var/tmp"
 then set! bufdisplay="syntax email"
}
if bufdisplay=="html" && readonly
then set locked
set e=""
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Some messages
if tolower(basename(program))!="elvis" && !ro && (bd=="html" || bd=="man" || bd="tex")
then message To see the source, hit ^Wd
if partiallastline && readeol!="binary"
then warning Partial last line
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" EXECUTE MODELINES, IF "modeline" OPTION IS SET
if modeline && buflines >= 1 && buflines <= modelines * 2
then %s/ex:\(.*\):/\1/x
if modeline && buflines > modelines * 2
then {
 eval 1,(modelines)s/[ev][xi]:\\\(.*\\\):/\1/x
 eval (buflines - modelines + 1),(buflines)s/[ev][xi]:\\\(.*\\\):/\1/x
}
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" RESTORE THE "saveregexp" OPTION
if s
then set saveregexp
set s=""
